def branchName = null
def jobName = null
def productsBuild = null
def outputDir = 'test-result'
def artifactFilterRcptt = 'gui-test-runner/result/**/*'

pipeline {
    agent any
    options {
        buildDiscarder(logRotator(numToKeepStr: '500'))
    }
    triggers {
        pollSCM 'H 1 * * 1-5'
    }
    stages {
        stage('Build products') {
            steps {
                script {
                    branchName = env.GIT_BRANCH.replace('origin/', '')
                    jobName = "verinice/${branchName.replace('/', '%2F')}"
                    productsBuild = build(
                      job: jobName,
                      propagate: false,
                      parameters: [
                        booleanParam(name: 'runRCPTTTests', value: false),
                        booleanParam(name: 'archiveIntegrationTestResults', value: true)
                      ]
                    )
                    copyArtifacts(
                      filter: '**/build/reports/**/*.xml,**/target/surefire-reports/*.xml',
                      projectName: jobName,
                      selector: specific(productsBuild.getNumber().toString()),
                      target: "${outputDir}/integration"
                    )
                }
            }
        }
        stage('Run UI tests') {
            parallel {
                stage('Run UI tests on Linux') {
                    steps {
                        runRCPTT 'rcptt-client-test', branchName, jobName, productsBuild.number, 'Linux', artifactFilterRcptt, "${outputDir}/gui-test-linux", 'GUI Test Report (Linux)'
                    }
                }
                stage('Run UI tests on macOS Current') {
                    steps {
                        runRCPTT 'rcptt-client-test', branchName, jobName, productsBuild.number, 'macOS current', artifactFilterRcptt, "${outputDir}/gui-test-mac-current", 'GUI Test Report (macOS)'
                    }
                }
                stage('Run UI tests on macOS Predecessor') {
                    steps {
                        runRCPTT 'rcptt-client-test', branchName, jobName, productsBuild.number, 'macOS predecessor', artifactFilterRcptt, "${outputDir}/gui-test-mac-predecessor", 'GUI Test Report (macOS)'
                    }
                }
                stage('Run UI tests on Windows') {
                    steps {
                        runRCPTT 'rcptt-client-test', branchName, jobName, productsBuild.number, 'Windows 10', artifactFilterRcptt, "${outputDir}/gui-test-windows", 'GUI Test Report (Windows 10)'
                    }
                }
                stage('Run 3-tier UI tests') {
                    steps {
                        runRCPTT 'rcptt-server-test', branchName, jobName,  productsBuild.number, null, artifactFilterRcptt, "${outputDir}/gui-test-3-tier", 'GUI Test Report (3-tier)'
                    }
                }
                stage('Run reporting tests') {
                    steps {
                        script {
                            def target = "${outputDir}/reporting-all"
                            runRCPTT 'rcptt-all-report-tests', branchName, jobName, productsBuild.number, null, 'test-result/**/*', target, null
                            archiveArtifacts artifacts: "${target}/**/*"
                            // FIXME enable again when VN-2882 is done
                            // createPerformancePlot("${target}/test-result/products_de/metric.csv", 'Product Reports')
                            createPerformancePlot("${target}/test-result/vn_de/metric.csv", 'Verinice Reports (DE)')
                            createPerformancePlot("${target}/test-result/vn_en/metric.csv", 'Verinice Reports (EN)')
                        }
                    }
                }
                stage('Run standalone performance tests') {
                    steps {
                        script {
                            def target = "${outputDir}/performance-test-standalone"
                            runRCPTT 'rcptt-client-performance-test', branchName, jobName, productsBuild.number, null, artifactFilterRcptt, target, 'GUI Test Report (Performance Standalone)'
                            createPerformancePlot("${target}/gui-test-runner/result/metric.csv", 'Standalone')
                        }
                    }
                }
                stage('Run 3-tier performance tests') {
                    steps {
                        script {
                            def target = "${outputDir}/performance-test-3-tier"
                            runRCPTT 'rcptt-server-performance-test', branchName, jobName, productsBuild.number, null, artifactFilterRcptt, target, 'GUI Test Report (Performance 3-tier)'
                            createPerformancePlot("${target}/gui-test-runner/result/metric.csv", '3-tier')
                        }
                    }
                }
            }
        }
        stage('Record results') {
            steps {
                // archiveArtifacts artifacts: "${outputDir}/**/*.xml"
                junit(
                  allowEmptyResults: true,
                  testDataPublishers: [[$class: 'StabilityTestDataPublisher']],
                  testResults: "${outputDir}/**/*.xml"
                )
                perfReport(
                  filterRegex: '',
                  modePerformancePerTestCase: true,
                  sourceDataFiles: "${outputDir}/**/*.xml"
                )
            }
        }
    }
}

def runRCPTT(jobName, branchName, jobNameArtifacts, buildNumberArtifacts, operatingSystem, copyArtifactsFilter, resultDir, htmlReportName){
    def rcpttBuild = build(
      job: jobName,
      propagate: false,
      parameters: [
        gitParameter(name: 'BRANCH_OR_TAG', value: branchName),
        string(name: 'job_to_copy_from', value: jobNameArtifacts),
        string(name: 'build_to_copy_from', value: """<SpecificBuildSelector plugin="copyartifact@1.45.1">  <buildNumber>${buildNumberArtifacts}</buildNumber></SpecificBuildSelector>"""),
        string(name: 'OPERATING_SYSTEM', value: operatingSystem)
      ]
    )
    copyArtifacts(
      filter: copyArtifactsFilter,
      fingerprintArtifacts: true,
      optional: true,
      projectName: jobName,
      selector: specific(rcpttBuild.getNumber().toString()),
      target: resultDir
    )
    if (htmlReportName != null){
        publishHTML([
          allowMissing: false,
          alwaysLinkToLastBuild: false,
          keepAll: true,
          reportDir: "${resultDir}/gui-test-runner/result",
          reportFiles: 'results.html',
          reportName: htmlReportName,
          reportTitles: branchName
        ])
    }
}


def createPerformancePlot(fileName, group){
    def records = readCSV file: fileName
    def data = records[1]
    def measurementLabels = data.collate(5).collect{it.first().trim()}.findAll{!it.empty}
    measurementLabels.each { label->
      callPlot fileName, label, group
    }
}

def callPlot(String fileName, String plotName, String group) {
    echo "Creating plot ${plotName}"

    plot csvFileName: 'plot-'+plotName+'.csv',
         csvSeries: [[displayTableFlag: false,
                      exclusionValues: plotName+'-avg,'+plotName+'-min,'+plotName+'-max',
                      file: fileName,
                      inclusionFlag: 'INCLUDE_BY_STRING',
                      url: '']],
         group: group,
         style: 'line',
         title: plotName
}
