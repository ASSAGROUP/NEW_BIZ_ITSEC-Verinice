--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _HQDeEI9NEeic2YXgdvyDUQ,_lGnj8Ix1Eei2UKUDmonytQ,_mCQwkI9ZEeic2YXgdvyDUQ
Element-Name: test-license-management-server
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _ehgZkPyjEeqYO5h9EZ3cUQ
Runtime-Version: 2.5.1.M3
Save-Time: 1/18/21, 11:17 AM
Tags: ISO, serverOnlyTest
Testcase-Type: ecl
Verifications: _wm5P0CtBEeuMltfUUtdvug

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

Test the license management by adding user who use the license until no more available.
Implement some "Lizenz-Management" testcases from the test protocol.
------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

global [val licenseDate "2031-01-01"]

proc "create-new-account" [val personname] [val count] {
	with [get-window Account] {
	    get-table | select $personname -column Element
	    get-button "Weiter >" | click
	    with [get-editbox -after [get-label "Login Name"]] {
	        set-text [concat "ln" $personname]
	    }
	    try {
	        with [get-editbox -after [get-label Kennwort]] {
	            set-text [decrypt "0sf0Ku9NJzUSf4qa+L0SaA=="]
	        }
	        with [get-editbox -after [get-label "Kennwort wiederholen"]] {
	            set-text [decrypt "0sf0Ku9NJzUSf4qa+L0SaA=="]
	        }
	    } -catch {
	        // when we disable the authentification in the webapp these textfields don't exist so we skip them silently
	    }
	    with [get-editbox -after [get-label "E-Mail"]] {
	        set-text aaaaa
	    }
	    
	    get-button "Weiter >" | click
	    get-button "Weiter >" | click
	    get-button "Weiter >" | click
	    screen-shot Account [concat "create-account-" $personname ".png"]
	    get-button [concat "1. MY_LIC - " $licenseDate " - (" $count "/5)"] | check
        screen-shot Account [concat "create-account-" $personname "-checked.png"]
	    get-button "Weiter >" | click
	    get-button "Weiter >" | click
	    get-button "Weiter >" | click
	    get-button Fertigstellen | click
	}
}


log "test-license-management"

ism-import-vna  MY_LIC.vna

// the the encrypted content
get-button Objektbrowser | click 
get-view ISM | get-tree | select "importierte Objekte/MyLic [64522f]/Controls [64522f]/Control [64522f]"
screen-shot-window "lic-content.png"
get-view Objektbrowser | get-control Browser | get-property text | matches ".*<div id=\"content\"><h1>Control</h1><p><p><b><font color=\"red\">Für diesen Benutzer ist keine Lizenz zugewiesen\.<br/>Bitte kontaktieren Sie Ihren Administrator, wenn Sie diesen Inhalt nutzen möchten\.</font></b></p></p></div>.*"
    | verify-true

get-button Accounts | click

// create 5 accounts
repeat [val counter] -times 5 -command {
        create-iso-person "importierte Objekte/MyLic [64522f]/Personen [64522f]" [concat "person-with-lic-" $counter]
        
        get-view Accounts | get-button "New Account..." | click 
        create-new-account [concat "person-with-lic-" $counter] $counter
}
// the next account cant not get a license
create-iso-person "importierte Objekte/MyLic [64522f]/Personen [64522f]" "person-with-lic-5"
get-view Accounts | get-button "New Account..." | click 
with [get-window Account] {
    get-table | select "person-with-lic-5" -column Element
    get-button "Weiter >" | click
    with [get-editbox -after [get-label "Login Name"]] {
        set-text [concat "lnperson-with-lic-5"]
    }
    try {
        with [get-editbox -after [get-label Kennwort]] {
            set-text [decrypt "0sf0Ku9NJzUSf4qa+L0SaA=="]
        }
        with [get-editbox -after [get-label "Kennwort wiederholen"]] {
            set-text [decrypt "0sf0Ku9NJzUSf4qa+L0SaA=="]
        }
    } -catch {
        // when we disable the authentification in the webapp these textfields don't exist so we skip them silently
    }
    with [get-editbox -after [get-label "E-Mail"]] {
        set-text aaaaa
    }
    
    get-button "Weiter >" | click
    get-button "Weiter >" | click
    get-button "Weiter >" | click
    get-button [concat "1. MY_LIC - " $licenseDate " - (5/5)"] | get-property "isEnabled()" | equals false | verify-true
    screen-shot Account "create-account-person-with-lic-5-disabled.png"
    get-button "Weiter >" | click
    get-button "Weiter >" | click
    get-button "Weiter >" | click
    get-button Fertigstellen | click
    
}
//remove the license from an account
get-view Accounts | get-table | select MyLic | double-click
with [get-window Account] {
    with [get-button "Weiter >"] {
        click 
        click 
        click 
    }
    get-button [concat "1. MY_LIC - " $licenseDate " - (5/5)"] | uncheck
    get-button Fertigstellen | click 
}
//add the license to another
get-view Accounts | get-table | select [get-item -path MyLic -index 5] | double-click
with [get-window Account] {
    with [get-button "Weiter >"] {
        click 
        click 
        click 
    }
    get-button [concat "1. MY_LIC - " $licenseDate " - (4/5)"] | check
    get-button Fertigstellen | click 
}
// so it cannot be added more
get-view Accounts | get-table | select MyLic | double-click
with [get-window Account] {
    with [get-button "Weiter >"] {
        click 
        click 
        click 
    }
    get-button [concat "1. MY_LIC - " $licenseDate " - (5/5)"] | get-property "isEnabled()" | equals false | verify-true
    get-button Fertigstellen | click 
}

// refresh the view
get-view Accounts | close
get-button Accounts | click
// and test the displayed license
with [get-view Accounts | get-table] {
    get-item -path MyLic | get-property "columns[11]" | equals "" | verify-true
    get-item -path MyLic | get-property "columns[3]" | equals "person-with-lic-0" | verify-true
    get-item -path MyLic -index 1 | get-property "columns[3]" | equals "person-with-lic-1" | verify-true
    get-item -path MyLic -index 1 | get-property "columns[11]" | equals X | verify-true
    get-item -path MyLic -index 2 | get-property "columns[3]" | equals "person-with-lic-2" | verify-true
    get-item -path MyLic -index 2 | get-property "columns[11]" | equals X | verify-true
    get-item -path MyLic -index 3 | get-property "columns[3]" | equals "person-with-lic-3" | verify-true
    get-item -path MyLic -index 3 | get-property "columns[11]" | equals X | verify-true
    get-item -path MyLic -index 4 | get-property "columns[3]" | equals "person-with-lic-4" | verify-true
    get-item -path MyLic -index 4 | get-property "columns[11]" | equals X | verify-true
    get-item -path MyLic -index 5 | get-property "columns[3]" | equals "person-with-lic-5" | verify-true
    get-item -path MyLic -index 5 | get-property "columns[11]" | equals X | verify-true
}

delete-iso-model "importierte Objekte/MyLic [64522f]"
log "end-license-management"

verify-error {get-window Fehler }

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
