--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _fKpG0MvFEei8QokgLeP3Yw,_HQDeEI9NEeic2YXgdvyDUQ,_tlZuIIx1Eei2UKUDmonytQ,_lGnj8Ix1Eei2UKUDmonytQ,_mCQwkI9ZEeic2YXgdvyDUQ
Element-Name: test_6_2
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _SlfjEGUPEeqN8MswvNs8LA
Runtime-Version: 2.5.3.202205021448
Save-Time: 8/25/22, 12:02 PM
Tags: ISM
Testcase-Type: ecl
Verifications: _wm5P0CtBEeuMltfUUtdvug

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "open-vlt" [val vlt] {
	set-dialog-result File [concat $datadir [get-java-property "file.separator"] $vlt]
	get-menu -path "Datei/Report-Abfrage/Abfrage öffnen..." | click
}

proc "exec-and-export" [val reportname] [val scopelist] [val filename] {
	get-editor $reportname | get-button "Abfrage ausführen (CSV)..." | click
	$scopelist | each [val name] {
		get-window -class CsvExportDialog | get-group "" | get-button $name | check
	}
	set-dialog-result File [concat $reportdir [get-java-property "file.separator"] $filename]
	with [get-window -class CsvExportDialog] {
		get-button "Datei wählen" | click
		get-button OK | click
	}
}

proc "try-dont-switch" {
	try -command {
		get-window "Wechseln zur zugeordneten Perspektive" | get-button Nein | click
	} -catch {
	}
}

proc "verify-element-exists" [val view] [val path] [val caption] {
    get-view $view | get-tree | get-item -path $path | get-property caption | equals $caption | verify-true
}

proc "wait-for-imports-or-fail" {
	get-menu -path "Ansicht/Zeige View.../Modernisierter IT-Grundschutz" | click
	try-dont-switch
	get-menu -path "Ansicht/Zeige View.../GS-Modell" | click
	try-dont-switch
	try -times 40 -delay 10000 -command {
		verify-element-exists ISM "importierte Objekte/Beispiel: NoKyTo GmbH 2018 \\[DP\\]"
			"Beispiel: NoKyTo GmbH 2018 [DP]"
		verify-element-exists "Modernisierter IT-Grundschutz"
			"importierte Objekte/MODPLAST \\[SerNet-DM\\]" "MODPLAST [SerNet-DM]"
		verify-element-exists Grundschutzmodell "importierte Objekte/  RECPLAST \\[SerNet\\]"
			"RECPLAST [SerNet]"
	} -finally {
        get-view "Modernisierter IT-Grundschutz" | close
        get-view Grundschutzmodell | close
	}
}

log "6.2: 6.2 started"
get-view Objektbrowser | close


open-vlt "TestReportISM.vlt"
open-vlt "TestReportMod.vlt"
open-vlt "TestReportAlt.vlt"

ism-import-vna "RECPLAST.vna"
ism-import-vna "Beispiel_NoKyTo_GmbH_2018.vna"
ism-import-vna "modplast-1.1.vna"

wait-for-imports-or-fail

log "6.2: starting to execute queries"

exec-and-export "TestReportAlt.vlt" [list "Beispiel: NoKyTo GmbH 2018" MODPLAST RECPLAST] "TestReportAlt-All.csv"
exec-and-export "TestReportAlt.vlt" [list "Beispiel: NoKyTo GmbH 2018"] "TestReportAlt.csv"

exec-and-export "TestReportMod.vlt" [list "Beispiel: NoKyTo GmbH 2018" MODPLAST RECPLAST] "TestReportMod-All.csv"
exec-and-export "TestReportAlt.vlt" [list MODPLAST] "TestReportMod.csv"

exec-and-export"TestReportISM.vlt" [list "Beispiel: NoKyTo GmbH 2018" MODPLAST RECPLAST] "TestReportISM-All.csv"
exec-and-export "TestReportAlt.vlt" [list RECPLAST] "TestReportISM.csv"


------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
