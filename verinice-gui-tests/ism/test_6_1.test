--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _fKpG0MvFEei8QokgLeP3Yw,_HQDeEI9NEeic2YXgdvyDUQ,_tlZuIIx1Eei2UKUDmonytQ,_lGnj8Ix1Eei2UKUDmonytQ,_mCQwkI9ZEeic2YXgdvyDUQ
Element-Name: test_6_1
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _xUiAIMeuEeipUK6ek91lPA
Runtime-Version: 2.5.1.M3
Save-Time: 9/17/20, 12:01 PM
Tags: ISM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "file-in-dir" [val dirname] [val filename] {
	concat $dirname [get-java-property "file.separator"] $filename
}

proc "file-in-report-dir" [val filename] {
	file-in-dir $reportdir $filename
}

proc "read-file-in-report-dir" [val filename] {
	read-file [uri-from-path [file-in-report-dir $filename]]
}

proc "read-file-in-data-dir" [val filename] {
	// work around https://bugs.eclipse.org/bugs/show_bug.cgi?id=510874
	read-file [uri-from-path [file-in-dir $datadir $filename]]
	  | invoke replace "Ã¤" "ä"
	  | invoke replace "Ã¼" "ü"
	  | invoke replace "Ã¼" "ü"
	  | invoke replace "\r" ""
}

proc "edit-empty-row" [val offset] [val selection1] [val selection2] [val selection3] {
	with [get-editor "Neue Abfrage [UNGÜLTIG]"] {
		get-combo -after [get-label "Verknüpfungen"] -index $offset | select $selection1
		get-combo -after [get-label "Verknüpfungen"] -index [plus $offset 1] | select $selection2
		get-combo -after [get-label "Verknüpfungen"] -index [plus $offset 3] | select $selection3
	}
}
proc "copy-edit-last" [val offset] [val selection] {
	with [get-editor "Neue Abfrage"] {
		get-button "Letzte Spalte kopieren" | click
		get-combo -after [get-label "Verknüpfungen"] -index $offset | select $selection
		get-button "Leere Spalte hinzufügen" | click
	}
}

proc "close-and-reopen-query" [val filename] {
	get-editor $filename | close
	set-dialog-result File [file-in-report-dir $filename]
	get-menu -path "Datei/Report-Abfrage/Abfrage öffnen..." | click
}

proc "save-report" [val filename] {
	set-dialog-result File [file-in-report-dir $filename]
	get-eclipse-window | key-type "M1+s"
}
proc "new-query" [val selection1] [val selection2] {
	get-menu -path "Datei/Report-Abfrage/Neue Abfrage" | click
	with [get-editor "Neue Abfrage [UNGÜLTIG]"] {
		get-combo -after [get-label "Verknüpfungen"] | select $selection1
		get-combo -after [get-label "Verknüpfungen"] -index 2 | select $selection2
	}
}
proc "copy-edit-end" [val selection] {
	with [get-editor "Neue Abfrage"] {
		get-button "Letzte Spalte kopieren" | click
		get-combo -after [get-label "Verknüpfungen"] -index 45 | select $selection
	}
}

proc "compare-vlt" [val fileName] {
	let [val actualContent [read-file-in-report-dir $fileName]] [val expectedContent [read-file-in-data-dir $fileName]] {
		$actualContent | equals $expectedContent | verify-true
	}
}

proc "compare-vlts" {
	compare-vlt "TestReportISM.vlt"
	compare-vlt "TestReportMod.vlt"
	compare-vlt "TestReportAlt.vlt"
}

log "test_6.1"
new-query "Asset (ISM: asset)" "Art des Assets"
copy-edit-last 5 Beschreibung
edit-empty-row 7 "/" "Szenario (ISM: incident_scenario)" Titel
copy-edit-last 15 "Erklärung"
edit-empty-row 17 ":" "Person (ISM: person-iso)" Titel
copy-edit-last 25 Beschreibung
edit-empty-row 27 "<" "Assets (ISM: assetgroup)" Titel
copy-edit-last 35 Beschreibung
edit-empty-row 37 ">" "Control (ISM: control)" "Abkürzung"
copy-edit-end Beschreibung
save-report "TestReportISM.vlt"

log "6.1: building 2nd query"

new-query "Anwendung (GS neu: bp_application)" "Kürzel"
copy-edit-last 5 Beschreibung
edit-empty-row 7 "/" "Anwendung (GS neu: bp_application)" "Kürzel"
copy-edit-last 15 Anzahl
edit-empty-row 17 ":" "IT-System (GS neu: bp_itsystem)" Titel
copy-edit-last 25 Beschreibung
edit-empty-row 27 "<" "Anwendungen (GS neu: bp_application_group)" Titel
copy-edit-last 35 Beschreibung
edit-empty-row 37 ">" "Gefährdungen (GS neu: bp_threat_group)" Titel
copy-edit-end Beschreibung
save-report "TestReportMod.vlt"

log "6.1: building 3rd query"

new-query "Client (GS alt: client)" "Kürzel"
copy-edit-last 5 "Erläuterung"
edit-empty-row 7 "/" "Anwendung (GS alt: anwendung)" "Kürzel"
copy-edit-last 15 "Erläuterung"
edit-empty-row 17 ":" "IT-Verbund (GS alt: itverbund)" Titel
copy-edit-last 25 Beschreibung
edit-empty-row 27 "<" "IT-Systeme: Clients (GS alt: clientskategorie)" Name
copy-edit-last 35 "<UUID>"
edit-empty-row 37 ">" "Risikoanalyse (GS alt: riskanalysis)" Name
copy-edit-end "<UUID>"
save-report "TestReportAlt.vlt"

close-and-reopen-query "TestReportISM.vlt"
close-and-reopen-query "TestReportMod.vlt"
close-and-reopen-query "TestReportAlt.vlt"

compare-vlts
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
